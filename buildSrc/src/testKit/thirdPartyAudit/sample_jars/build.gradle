/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

plugins {
  id 'java'
}
repositories {
  mavenCentral()
}

dependencies {
  implementation "org.apache.logging.log4j:log4j-core:2.24.3"
}

["0.0.1", "0.0.2"].forEach { v ->
  ["opensearch", "other"].forEach { p ->
    tasks.register("dummy-${p}-${v}", Jar) {
      destinationDirectory = file("${buildDir}/testrepo/org/${p}/gradle/dummy-io/${v}/")
      archiveFileName = "dummy-io-${v}.jar"
      from sourceSets.main.output
      include "**/TestingIO.class"
      if (v == "0.0.2") {
        manifest {
          attributes(
            "X-Different": "Different manifest, different jar"
          )
        }
      }
    }
    build.dependsOn("dummy-${p}-${v}")
  }
}

["0.0.1"].forEach { v ->
  ["opensearch", "other"].forEach { p ->
    tasks.register("broken-log4j-${p}-${v}", Jar) {
      destinationDirectory = file("${buildDir}/testrepo/org/${p}/gradle/broken-log4j/${v}/")
      archiveFileName = "broken-log4j-${v}.jar"
      from sourceSets.main.output
      include "**/TestingLog4j.class"
    }
    build.dependsOn("broken-log4j-${p}-${v}")
  }
}

// Must configure a single task for these test jars to ensure they are unzipped to the same directory
tasks.register("force-collision", Jar) {
  destinationDirectory = file("${buildDir}/testrepo/org/other/gradle/collision-test/0.0.1/")

  doFirst {
    def destDir = destinationDirectory.get().asFile
    destDir.mkdirs()

    // Create temporary directories for building the jars
    def tempDir1 = new File(temporaryDir, "jar1")
    def tempDir2 = new File(temporaryDir, "jar2")  // Fixed typo here

    // Setup first jar with LICENSE file
    def metaInf1 = new File(tempDir1, "META-INF")
    metaInf1.mkdirs()
    new File(metaInf1, "LICENSE").text = "Test License Content"

    // Setup second jar with LICENSE directory
    def metaInf2 = new File(tempDir2, "META-INF/LICENSE")
    metaInf2.mkdirs()
    new File(metaInf2, "content.txt").text = "Test Content"

    // Create the actual jar files
    ant.jar(destfile: new File(destDir, "dummy-license-file.jar")) {
      fileset(dir: tempDir1)
    }

    ant.jar(destfile: new File(destDir, "dummy-license-dir.jar")) {
      fileset(dir: tempDir2)
    }
  }
}

tasks.register("jarhellJdk", Jar) {
  destinationDirectory = file("${buildDir}/testrepo/org/other/gradle/jarhellJdk/0.0.1/")
  archiveFileName = "jarhellJdk-0.0.1.jar"
  from sourceSets.main.output
  include "**/String.class"
  into "java/lang"
}

build.dependsOn("jarhellJdk", "force-collision")
